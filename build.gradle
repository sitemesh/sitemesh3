plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0-rc-1"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

allprojects  {
    group = 'org.sitemesh'
    version = '3.2.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral() // Use Maven Central Repo to search for Dependencies
    }
}

subprojects {
    apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'java'

    afterEvaluate {
        jar {
            manifest {
                attributes(
                        'Specification-Title' : project.ext.title,
                        'Specification-Version': project.version
                )
            }
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(project.components.java)
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                }
                pom {
                    name = "${project.name}"
                    description = 'SiteMesh is a web-page layout and decoration framework and web- application integration framework to aid in creating sites consisting of many pages for which a consistent look/feel, navigation and layout scheme is required.'
                    url = 'https://github.com/sitemesh/sitemesh3'
                    licenses {
                        license {
                            name =  'The Apache Software License, Version 2.0'
                            url =  'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution =  'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'joewalnes'
                            name = 'Joe Walnes'
                            email = 'joe dot walnes at gmail'
                        }
                        developer {
                            id = 'rburton'
                            name = 'Richard L. Burton III'
                            email = 'rburton@codehaus.org'
                        }
                        developer {
                            id = 'codeconsole'
                            name = 'Scott Murphy'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/sitemesh/sitemesh3.git'
                        developerConnection = 'scm:git:git@github.com:sitemesh/sitemesh3.git'
                        url = 'https://github.com/sitemesh/sitemesh3/'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/sitemesh/sitemesh3/issues'
                    }
                }
            }
        }
    }
    
    signing {
        sign publishing.publications.mavenJava
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
